plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dev.flutter.flutter-gradle-plugin'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace = 'app.musily.music'
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion
    flavorDimensions 'default'

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = 'app.musily.music'
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
    }

    buildFeatures {
        buildConfig = true
    }

    signingConfigs {
        dev {
            storeFile = file(keystoreProperties['devStoreFile'])
            storePassword = keystoreProperties['devStorePassword']
            keyAlias = keystoreProperties['devKeyAlias']
            keyPassword = keystoreProperties['devKeyPassword']
        }

        stable {
            storeFile = file(keystoreProperties['stableStoreFile'])
            storePassword = keystoreProperties['stableStorePassword']
            keyAlias = keystoreProperties['stableKeyAlias']
            keyPassword = keystoreProperties['stableKeyPassword']
        }
    }

    buildTypes {
        release {
            shrinkResources = false
            signingConfig signingConfigs.stable
        }
        debug {
            signingConfig signingConfigs.dev
        }
    }

    productFlavors {
        dev {
            dimension 'default'
            resValue 'string', 'app_name', 'Musily Dev'
            applicationId 'app.musily.music.dev'
            applicationIdSuffix '.dev'
            manifestPlaceholders = [
                appIcon: '@mipmap/launcher_icon_dev',
            ]
            signingConfig signingConfigs.dev
        }
        stable {
            dimension 'default'
            resValue 'string', 'app_name', 'Musily'
            manifestPlaceholders = [
                appIcon: '@mipmap/launcher_icon',
            ]
            signingConfig signingConfigs.stable
        }
    }
}

flutter {
    source = '../..'
}
